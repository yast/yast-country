/*
 *
 * Module:             language.ycp
 *
 * Author:             Klaus Kaempf (kkaempf@suse.de)
 *
 * Submodules:
 *
 *
 * Purpose:	configure language in running system
 *
 * Modify:
 *
 *
 * $Id$
 */

{
    textdomain "country";

    import "Language";
    import "Wizard";

    import "Popup";
    import "Label";
    import "Encoding";

    // Memozize the current language.
    //
    string language_on_entry = Language::language;

    y2milestone("language_on_entry: <%1>", language_on_entry );

    // create the wizard dialog
    Wizard::CreateDialog();

    any result = `again;

    Wizard::OpenAcceptDialog();		// Reduce flicker (yes, it's a kludge)

    while (result == `again)
    {
	Wizard::OpenAcceptDialog();	// This dialog will actually be used

	// Params are:		 	      `back `next  set_default
        result = WFM::CallFunction ("inst_language", [true, true, false]);

	Wizard::CloseDialog();
    }

    Wizard::CloseDialog();

    Wizard::ReplaceButtonBox( Wizard::HasAcceptButton() ?
			      Wizard::AbortAcceptButtonBox() :
			      Wizard::BackAbortNextButtonBox() );

    y2milestone ("result '%1'", result);

    if( result == `cancel || result == `back )
	{
	// Back to original values...
	//
	y2milestone( "`cancel or `back --> restoring: <%1>", language_on_entry );

	import "Console";
	import "Installation";

	boolean use_utf8 = true;		// utf8 is default

	if( ! lookup (UI::GetDisplayInfo(), "HasFullUtf8Support", true ) )
	    {
	    use_utf8 = false;		// fallback to ascii
	    }

	// Set it in the Language module.
	//
	Language::Set( language_on_entry );

	// Set Console font
	//
	Console::SelectFont( language_on_entry );

	Installation::encoding = (use_utf8) ? "UTF-8" : Encoding::console;

	// Set it in YaST2
	Language::WfmSetLanguage();
	}
    else
	{
	// User wants to keep his changes --> save to sysconfig.
	//
	if( Language::language != language_on_entry || Language::ExpertSettingsChanged )
	    {
	    Language::Save();
	    }

	if( Language::language != language_on_entry )
	    {
	    y2milestone( "Language changed --> saving" );
	    // According to ro@suse.de it doesn't make sense to run individual
	    // SuSEcongig modules only because the set of modules and the variables
	    // they contain may change any time.
	    // ==> full SuSEconfig run
	    //
	    //SCR::Execute (.target.bash, "/sbin/SuSEconfig --quick --nonewpackage");
	    // propagate language to Shell
	    //SCR::Execute (.target.bash, "/sbin/SuSEconfig --module profiles");
	    // propagate language to ispell
	    //SCR::Execute (.target.bash, "/sbin/SuSEconfig --module ispell");
	    // propagate language to susewm
	    //SCR::Execute (.target.bash, "/sbin/SuSEconfig --module susewm");
	    // propagate language to KDE2
	    //SCR::Execute (.target.bash, "/sbin/SuSEconfig --module kdm3");

	    WFM::CallFunction( "inst_suseconfig", [ false, false ] );
	    // popup text
	    Popup::Message( _("Your language setting has been changed.

If necessary, you may want to adapt your keyboard settings to the new
language. This is possible either in the YaST2 Control Center or by 
starting \"yast2 keyboard\" directly."));
	    }
	else
	    {
	    y2milestone( "Language not changed --> doing nothing" );
	    }
	}

    return UI::CloseDialog();
}
