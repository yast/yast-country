/**
 * File:	modules/Language.ycp
 * Module:	Language
 * Summary:	This module does all language related stuff:
 * Authors:	Klaus Kaempf <kkaempf@suse.de>
 *		Thomas Roelz <tom@suse.de>
 *
 * $Id$
 */

{

module "Language";
textdomain "country";


import "Misc";
import "Mode";
import "Installation";
import "AsciiFile";
import "X11Version";
import "XF86Path";


/**
 * currently selected language
 */
global string language = "en_US";


/**
 * user readable description of language
 */
string name = "English (US)";

/**
 * Default language to be restored with MakeProposal.
 */
string default_language = "en_US";


/**
 * Default settings for ROOT_USES_LANG in /etc/sysconfig/language 
 */
string rootlang = "ctype";

/**
 * Use utf8 in locale 
 */
boolean use_utf8 = true;

global boolean ExpertSettingsChanged = false;


/**
 * Constructor
 *
 * Initializes module either from /etc/install.inf
 * or from /etc/sysconfig/language
 */
global define void Language() 
    ``{
    if( Mode::initial )
	{
	string lang = SCR::Read( .content.LANGUAGE );
	if( SCR::Read( .target.size, "/etc/install.inf") > 0 )
	    {
	    lang = SCR::Read (.etc.install_inf.Locale);
	    }
	if( lang == nil) 
	    lang = "";
    	y2milestone( "lang %1 Installation::defaultLanguage %2", lang, 
	             Installation::defaultLanguage );
	if( lang == "" )
	    {
	    lang = Installation::defaultLanguage;
	    }
	Set( lang );		// coming from /etc/install.inf
	SetDefault();		// also default
	}
    else
	{
	string local_lang =
	    Misc::SysconfigRead(.sysconfig.language.RC_LANG, "" );
	y2milestone( "reading RC_LANG %1", local_lang );
	if( size(local_lang)==0 )
	    {
	    // Try to read the old YaST1 language and convert setting.
	    string y1lang =
		Misc::SysconfigRead(.sysconfig.language.DEFAULT_LANGUAGE, "" );
	    map lang2iso = get_lang2iso();
	    if( size(y1lang)>0 && size(lang2iso[y1lang]:"")>0 )
		{
		local_lang = lang2iso[y1lang]:"";
		}
	    }
	if( size(local_lang)==0 )
	    {
	    local_lang = language;
	    }

	integer pos = findfirstof( local_lang, ".@" );

	if( pos >= 0 )
	    local_lang = substring( local_lang, 0, pos );

	y2milestone( "setting language to %1", local_lang );

	Set( local_lang );

	SetDefault();		// also default
	}
    if( SCR::Read( .target.size, "/etc/sysconfig/language" )>0 )
	{
	rootlang = Misc::SysconfigRead( .sysconfig.language.ROOT_USES_LANG, 
                                        rootlang );
	if( !Mode::initial )
	    {
	    string val = toupper( Misc::SysconfigRead( .sysconfig.language.RC_LANG,
						       "" ));
	    use_utf8 = find( val, ".UTF-8" )>0;
	    }
	else
	    {
	    use_utf8 = false;
	    }
	}
    }

/*
 * GetExpertValues()
 *
 * Return the values for the various expert settings in a map
 *
 * @param       -
 *
 * @return  map with values filled in
 *
 */

global define map GetExpertValues()
    ``{
    map ret = $[ "rootlang"  : rootlang,
                 "use_utf8"  : use_utf8 ];
    return( ret );
    }

/*
 * SetExpertValues()
 *
 * Return the values for the various expert setting in a map
 *
 * @param       val     map with new values of expert settings
 *
 * @return  void
 *
 */

global define void SetExpertValues( map val )
    ``{
    if( haskey(val,"rootlang") && size(val["rootlang"]:"")>0 )
        {
        rootlang = val["rootlang"]:"";
        }
    if( haskey(val,"use_utf8") )
        {
        use_utf8 = val["use_utf8"]:"";
        }
    }




/**
 * Return proposal string.
 *
 * @return	string	user readable description.
 *		If force_reset is true reset the module to the language
 *		stored in default_language.
 */
global define string MakeProposal( boolean force_reset, boolean language_changed ) ``{

    y2milestone("force_reset: %1", force_reset);
    y2milestone("language_changed: %1", language_changed);

    if ( force_reset ) Set( default_language );	// reset

    return name;
}


/**
 * Store current language as default language.
 */
global define string SetDefault() ``{
    y2milestone("Setting default language: %1", language);
    default_language = language;
    return;
}


/**
 * Return a map of ids and names to build up a selection list
 * for the user. The key is used later in the Set function
 * to select this language. The name is a translated string.
 *
 * @return map of $[ language : [ utf8-name, ascii-name] ...]
 *			for all known languages
 *			'language' is the (2 or 5 char)  ISO language code.
 *			'utf8-name' is a user-readable (UTF-8 encoded !) string.
 *			'ascii-name' is an english (ascii encoded !) string.
 * @see Set
 */
global define map Selection() ``{

    map languages_map = get_languages_map();

    return mapmap (string code, list data, languages_map, ``{
	return [code, [data[0]:"", data[1]:""]];
    });
}


/**
 * Set module to selected language.
 * @param lang language string ISO code of language
 */
global define void Set( string lang ) 
    ``{
    y2milestone( "lang:%1", lang );

    if( language != lang )	// Do it only if different
	{
	map languages_map = get_languages_map();

	if( !haskey( languages_map, lang ) && size(lang)>0 )
	    {
	    lang = substring( lang, 0, 2 );
	    boolean found = false;
	    foreach( `k, `e, languages_map,
		``{
		if( !found && substring( k, 0, 2)==lang )
		    {
		    found = true;
		    lang = k;
		    }
		});

	    }
	name = languages_map[lang, 0]:"English";
	language = lang;
	}
    if( Mode::initial )
	{
	map yinf = $[];
	AsciiFile::SetDelimiter( yinf, " " );
	AsciiFile::ReadFile( yinf, "/etc/yast.inf" );
	list lines = AsciiFile::FindLineField( yinf, 0, "Language:" );
	if( size(lines)>0 )
	    {
	    AsciiFile::ChangeLineField( yinf, lines[0]:-1, 1, language );
	    }
	else
	    {
	    AsciiFile::AppendLine( yinf, ["Language:", language] );
	    }
	AsciiFile::RewriteFile( yinf, "/etc/yast.inf" );
	}
    }


/**
 * Save state to target.
 */
global define void Save() ``{

    map languages_map = get_languages_map();

    list language_info = languages_map[language]:[];

    // full language code
    string val = language;
    if( use_utf8 )
	{
	val = val + language_info[2]:"";
	}
    else
	{
	val = val + language_info[3]:"";
	}

    SCR::Write(.sysconfig.language.RC_LANG, val );
    SCR::Write(.sysconfig.language.ROOT_USES_LANG, rootlang );

    SCR::Write(.sysconfig.language, nil);

    // Change the respective XF86Config content to match the current settings.
    // Do this only in the running system _and_ if XFree4 is installed.
    if ( Mode::normal && X11Version::version == "4" )
    {
	y2milestone("Storing current settings into XF86Config file (XFree4)." );
	XF86Path::update();
    }

    y2milestone("Saved data for language: <%1>",
	    language
	    + language_info[2]:""	// mandatory-LANG-modifier
	    + language_info[3]:"");	// optional-LANG-modifier
}


/**
 * Get the language DB
 * @return Language DB
 */
define map get_languages_map() ``{

    // The system_language DB
    //
    map languages_map = SCR::Read (.target.yast2, ["language.ycp", $[]]);

    if ( languages_map == nil ) languages_map = $[];

    return( languages_map );
}


/**
 * Get the map for conversion of old lang codes to new ISO-codes
 * (since Rel. 8.0)
 *
 * @return conversion map
 */
define map get_lang2iso() ``{

    // The old_lang --> ISO conversion map.
    //
    map lang2iso = SCR::Read( .target.yast2, "lang2iso.ycp");

    if ( lang2iso == nil ) lang2iso = $[];

    return( lang2iso );
}


/**
 * de_DE@UTF-8 -> "DE"
 * @return country part of language
 */
global define string GetLanguageCountry() ``{

    string lang = Language::language;
    if(lang == nil || lang == "")
        lang = Language::default_language;
    if(lang != nil && lang != "")
        if(find(lang, "@") != -1)
            lang = select(splitstring(lang, "@"), 0, "");
    if(lang != nil && lang != "")
        if(find(lang, "_") != -1)
            lang = select(splitstring(lang, "_"), 1, "");
	else
	    lang = toupper(lang);

    y2debug("language=%1",lang);
    return lang;
}

/* EOF */
}
